apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'maven-publish'

group = 'org.apcdevpowered'
version = '1.0.0'
archivesBaseName = 'javacc-gradle-plugin'

repositories { mavenCentral() }

dependencies {
    compile gradleApi()
    compile 'org.codehaus.groovy:groovy-all:2.4.3'

    compile 'commons-io:commons-io:2.4'

    compile 'net.java.dev.javacc:javacc:6.1.2'
    compile files('src/main/resources/jtb/jtb-1.4.0.jar')
}

compileGroovy {
    options.encoding = 'UTF-8'
    sourceCompatibility = '1.7'
    targetCompatibility = '1.7'
}

task sourceJar(type: Jar) {
    description = 'An archive of the source code of JavaCC Gradle Plugin'
    classifier = 'sources'
    from sourceSets.main.groovy
}

task javadocJar(type: Jar) {
    description = 'An archive of the javadoc for JavaCC Gradle Plugin'
    classifier = 'javadoc'
    from groovydoc
}

artifacts { archives javadocJar, sourceJar }

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourceJar { classifier 'sources' }
            artifact javadocJar { classifier 'javadoc' }
        }
    }
}

eclipse {
    classpath {
        downloadJavadoc = true
        downloadSources = true
    }
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}